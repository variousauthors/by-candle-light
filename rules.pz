title By Candle Light
author arrogant.gamer
homepage www.puzzlescript.net

run_rules_on_level_start
debug

========
OBJECTS
========

Background
LIGHTGREEN GREEN
11111
01111
11101
11111
10111


Target
DarkBlue
.....
.000.
.0.0.
.000.
.....

Wall
BROWN DARKBROWN
00010
11111
01000
11111
00010

Player
Black Orange White Blue
.000.
.111.
22222
.333.
.3.3.

Crate
Orange Yellow
00000
0...0
0...0
0...0
00000

night
black
00000
00000
00000
00000
00000

shadeU
#101010
.0.0.
0.0.0
.....
.....
.....

shadeD
#101010
.....
.....
.....
.0.0.
0.0.0

shadeL
#101010
0....
.0...
0....
.0...
0....

shadeR
#101010
....0
...0.
....0
...0.
....0

shadeUR
#101010
0.0.0
.0.0.
....0
...0.
....0

shadeUL
#101010
0.0.0
.0.0.
0....
.0...
0....

shadeDR
#101010
....0
...0.
....0
.0.0.
0.0.0

shadeDL
#101010
0....
.0...
0....
.0.0.
0.0.0

shadowUD
black
.0.0.
0.0.0
00000
0.0.0
.0.0.

shadowLR
black
.000.
0.0.0
.000.
0.0.0
.000.


candlestand
brown white yellow
.....
..1..
..1.2
.000.
.....

candle
brown white yellow orange
..3..
..1..
..1.2
.000.
.....

marker
transparent

side
transparent

corner
transparent

=======
LEGEND
=======

foreground = wall or player or crate or target
lightsource = player or candle
horizon = night
shade = shadeU or shadeD or shadeR or shadeL or shadeUR or shadeUL or shadeDR or shadeDL
border = wall
shadow = shadowLR or shadowUD

. = Background
# = Wall
P = Player
* = Crate
@ = Crate and Target
O = Target
| = candlestand

=======
SOUNDS
=======

Crate MOVE 36772507

================
COLLISIONLAYERS
================

Background
Target
Player, Wall, Crate, candle, candlestand
night, shade, shadow
marker, side, corner


======
RULES
======


[ >  Player | Crate ] -> [  >  Player | > Crate  ]
[ action player | candlestand ] -> [ player | candle ]
[ action player | candle ] -> [ player | candlestand ]

[ no night ] -> [ night ]

( lighting for each lightsource; grouped so they don't interfere )

late [ night lightsource ] -> [ no night marker lightsource ]

late [ night | marker lightsource ] -> [ marker no night | marker lightsource ]
late [ marker lightsource ] -> [ no marker lightsource ]

late UP [ marker no night | night ] -> [ marker no night | marker night ]
late RIGHT [ marker no night | marker night ] -> [ marker no night | no night ]

late UP [ marker no night | night ] -> [ marker no night | marker night ]
late LEFT [ marker no night | marker night ] -> [ marker no night | no night ]

late DOWN [ marker no night | night ] -> [ marker no night | marker night ]
late RIGHT [ marker no night | marker night ] -> [ marker no night | no night ]

late DOWN [ marker no night | night ] -> [ marker no night | marker night ]
late LEFT [ marker no night | marker night ] -> [ marker no night | no night ]

late [ marker ] -> [ ]

( shade the edges )

late [ no marker no night no lightsource no shade ] -> [ marker no night no lightsource ]
( shade the sides that are close to the horizon )
late UP [ marker | horizon ] -> [ marker shadeU | horizon ]
late DOWN [ marker | horizon ] -> [ marker shadeD | horizon ]
late LEFT [ marker | horizon ] -> [ marker shadeL | horizon ]
late RIGHT [ marker | horizon ] -> [ marker shadeR | horizon ]
( shade the corners )
late UP [ shadeL | horizon ] -> [ shadeUL | horizon ]
late DOWN [ shadeL | horizon ] -> [ shadeDL | horizon ]
late UP [ shadeR | horizon ] -> [ shadeUR | horizon ]
late DOWN [ shadeR | horizon ] -> [ shadeDR | horizon ]

late [ marker ] -> [ ]

( light up the walls to distinguish them )
late [ border shade ] -> [ border ]

late horizontal [ shade | night | shade ] -> [ shade | shadowLR | shade ]
late vertical [ shade | night | shade ] -> [ shade | shadowUD | shade ]

==============
WINCONDITIONS
==============

=======
LEVELS
=======

##########
#...#....#
#.|.#....#
#...#....#
#........#
#....|...#
#.P#.....#
#..#.....#
##########

